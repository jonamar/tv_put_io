apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply from: '../release_notes.groovy'

import com.github.triplet.gradle.androidpublisher.ReleaseStatus

def getPlayCredentials = { ->
    project.hasProperty('play_store_credentials') ? play_store_credentials : file("play-store-credentials.json").path
}

def getKeystorePath = { ->
    project.hasProperty('keystore') ? keystore : file("keystore.jks").path
}

def getKeystorePassword = { ->
    project.hasProperty('keystore_password') ? keystore_password : ""
}

def getKeystoreKeyPassword = { ->
    project.hasProperty('keystore_key_password') ? keystore_key_password : ""
}

def getKeystoreKeyAlias = { ->
    project.hasProperty('keystore_key_alias') ? keystore_key_alias : ""
}

def getBuildNumber = { ->
    project.hasProperty('build_number') ? build_number : 0
}

def getOutputDirectory = { ->
    project.hasProperty('output_directory') ? output_directory : "build/outputs/bundle/release"
}

android {
    compileSdkVersion 33
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "io.smileyjoe.putio.tv"
        minSdkVersion 25
        targetSdkVersion 33
        versionCode 41
        versionName "3.10.1." + getBuildNumber()
        buildConfigField "String", "PUTIO_CLIENT_ID", "\"${putio_client_id}\""
        buildConfigField "String", "PUTIO_CLIENT_SECRET", "\"${putio_client_secret}\""
        buildConfigField "String", "TMDB_AUTH_TOKEN", "\"${tmdb_auth_token}\""
        project.ext.set('host', 'tv.putio.smileyjoe.io')

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    signingConfigs {
        release {
            storeFile = file(getKeystorePath())
            storePassword getKeystorePassword()
            keyAlias getKeystoreKeyAlias()
            keyPassword getKeystoreKeyPassword()
        }
    }

    buildTypes {
        debug{
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue 'string', 'host_name', 'debug.' + project.ext.get('host')
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue 'string', 'host_name', project.ext.get('host')
        }
    }

    buildFeatures {
        viewBinding true
    }
}

play {
    track.set("internal")
    releaseStatus.set(ReleaseStatus.COMPLETED)
    artifactDir.set(file(getOutputDirectory()))
    serviceAccountCredentials.set(file(getPlayCredentials()))
    defaultToAppBundles.set(true)
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.leanback:leanback:1.0.0'

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.github.bumptech.glide:glide:4.13.0'
    implementation 'com.google.android.exoplayer:exoplayer:2.17.1'
    implementation 'com.google.android.exoplayer:extension-leanback:2.17.1'

    implementation 'com.github.HaarigerHarald:android-youtubeExtractor:v2.1.0'

    implementation 'com.koushikdutta.ion:ion:3.1.0'
    implementation "androidx.room:room-runtime:2.4.2"
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.tvprovider:tvprovider:1.1.0-alpha01'
    annotationProcessor "androidx.room:room-compiler:2.4.2"

    implementation 'com.prof18.rssparser:rssparser:4.0.2'

    // Required for Java 8 Stream APIs on Android API < 26
    // AGP 7.0.4 only supports desugar_jdk_libs 1.1.x (1.2+ requires newer AGP)
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}